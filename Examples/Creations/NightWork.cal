{
    Author: Samuele Giraudo
    Title: Night Work
    Creation: aug. 2020
}


put layout = 2 1 2 2 1 3 1 in
put root = 0 12 -2 in

put time = 2 1 in
put duration = 550 in

put synthesizer = 0.05 0.2 2000 15 30 in

put effect = delay 110 0.3 in
put effect = tremolo 550 0.8 in

{4 units}
let figure_1_A = ((0<< # 2<< # 4<< # 2'<<) # (2 * 4 * 2 * 0)) in
let figure_2_A = (0<< # 4<<) # (4 * 3 * 2 * 1) in
let figure_1_B = ((0<< # 2<< # 4<< # 6'<<) # (2 * 6 * 2 * 0)) in
let figure_2_B = (0<< # 6<<) # (6 * 5 * 4 * 3) in

let piece =
    begin repeat 2
        begin repeat 2
            (figure_1_A @@ 0) *
            (figure_1_A @@ 1) *
            (figure_1_A @@ 2) *
            (figure_1_A @@ -2)
        end
        *
        (figure_1_B @@ 0>) *
        (figure_1_B @@ 1>) *
        (figure_1_B @@ 2>) *
        (figure_1_B @@ -2>)
    end
    *
    begin repeat 2
        ((figure_2_A * figure_2_B) @@ 0) *
        ((figure_2_A * figure_2_B) @@ 1) *
        ((figure_2_A * figure_2_B) @@ 2) *
        ((figure_2_A * figure_2_B) @@ -2) *
        begin repeat 2
            (figure_1_B @@ 0>) *
            (figure_1_B @@ 1>) *
            (figure_1_B @@ 2>) *
            (figure_1_B @@ -2>)
        end
    end
    *
    begin repeat 2
        (figure_1_A @@ 0) *
        (figure_1_A @@ 1) *
        (figure_1_A @@ 2) *
        (figure_1_A @@ -2)
    end
    *
    (0<< # 2<< # 4<<)
in

piece

