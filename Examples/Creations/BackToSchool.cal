{
    Author: Samuele Giraudo
    Title: Back to School
    Creation: aug. 2020
}

put layout = 4 1 2 4 1 in
put root = 3 12 -2 in

put time = 2 1 in
put duration = 250 in

put synthesizer = 0.12 0.45 2250 50 100 in
put effect = delay 125 0.24 in
put effect = tremolo 125 0.85 in 

{8 units}
let figure_1_A = 0 * 1 * 2 * 3 * 2 * 1 * 0< in
let figure_2_A = figure_1_A @7 2 in

{4 units}
let figure_1_B = 0 * 3> * 2> * 1> * 4> * 3> * 2> in
let figure_2_B = figure_1_B @@ 2 in

{8 units}
let phrase_1 =
    0>  * 1>  * 3> *
    1>  * 3>  * 0'> *
    3>  * 0'> * 1'> *
    0'> * 1'> * 3'> *
    1'> * 3'> * 0''> *
    3'>
in

let phrase_2 =
    3>  * 1>  * 0> *
    0'>  * 3>  * 1> *
    1'>  * 0'> * 3> *
    3'> * 1'> * 0'> *
    0''> * 3'> * 1'> *
    3'>
in

{8 units}
let background_1 = repeat 4 ((0, # 3,) * .) in
let background_2 = (0,<< # 3,<<) * . * (0,< # 3,<) * . in

{16 units}
let cell_1 = (figure_1_A * figure_2_A) # (background_1 * background_1) in
let cell_2 = (figure_1_A * figure_2_A) # (background_2 * background_2) in

{8 units}
let cell_3 = ((figure_1_B * figure_2_B) # background_1) in
let cell_4 = ((figure_1_B * figure_2_B) # background_2) in

{16 units}
let cell_5 = (phrase_1 * (reverse phrase_2)) # (background_1 * background_1) in

let piece =
    begin
        repeat 2
        background_1 *
        cell_1 * (cell_1 @@ 2) * (cell_1 @@ 1) *
        cell_2 * (cell_2 @@ 2) * (cell_2 @@ 1) *
        cell_3 * (cell_3 @@ 2) * (cell_3 @@ 1) *
        cell_4 * (cell_4 @@ 2) * (cell_4 @@ 1) *
        cell_3 * (cell_3 @@ 2) * (cell_3 @@ 1) *
        (repeat 2 cell_5)
    end
    *
    cell_4 * (cell_4 @@ 2) * (cell_4 @@ 1) *
    (repeat 2 cell_5) *
    cell_1 * (cell_1 @@ 2) * (cell_1 @@ 1) *
    cell_2 * (cell_2 @@ 2) * (cell_2 @@ 1) *
    figure_1_A
in

piece

