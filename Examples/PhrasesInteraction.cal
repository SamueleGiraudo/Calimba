{
    Author: Samuele Giraudo
    Creation: dec. 2020
    Modifications: dec. 2020, jan. 2021
}

{Sets the minor pentatonic layout.}
put layout = 3 2 2 3 2 in

{Sets the A 110 as root note.}
put root = 0 12 -2 in

{Sets the time shape.}
put time = 2 1 in

{Sets the unit duration.}
put duration = 300 in

{Sets a synthesizer for crisp sounds.}
put synthesizer = 0.4 0.6 600 50 20 in

{Add a slight delay effect.}
put effect = delay 150 0.33 in

{A first phrase on 10 units of time. Some atoms are named by x or by y.}
let p1 = 0< * 1 * 2 * 0:x * 1:x * 2:x * -1:y * 0:y< in

{Three phrases obtained from p1, on 10 units of time. These phrases are obtained by labeled
 insertion from the phrase p1.}
let p2_1 = (p1 @x 0') @y 3 in
let p2_2 = (p1 @x 0') @y 2 in
let p2_3 = (p1 @x 0') @y 0' in

{A second phrase on 30 units of time.}
let p2 = p2_1 * p2_2 * p2_3 in

{A phrase, a bass line, on 10 units of time.}
let p3 = 0,< * .< * 0,< * .< * 0 * 0' in

{The tree phrases are finally repeated and stacked to be played.}
(repeat 48 p1) # (repeat 16 p2) # (repeat 48 p3)

